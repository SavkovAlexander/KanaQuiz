apply plugin: 'com.android.application'
//ref: https://stackoverflow.com/a/49739351/3582371
if (getGradle().getStartParameter().getTaskNames().toString().toLowerCase().contains('release')) {
    apply plugin: 'io.fabric'
    apply plugin: 'com.google.firebase.firebase-perf'
}

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'
    defaultConfig {
        applicationId 'com.noprestige.kanaquiz'
        minSdkVersion 17
        targetSdkVersion 28
        versionCode 16
        versionName '0.13.1'
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ['room.schemaLocation':
                                     "$projectDir/schemas".toString()]
            }
        }
    }
    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }
    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            testCoverageEnabled true
            applicationIdSuffix '.dev'
            versionNameSuffix ' (dev)'
            debuggable true
        }
        pure {
            initWith release
            //TODO: Get this working.
            //manifestPlaceholders = [mainClassName:'.KanaQuizPure']
            versionNameSuffix '-pure'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0'
    implementation 'androidx.appcompat:appcompat:1.0.2'
    testImplementation 'junit:junit:4.12'
    implementation 'com.google.android.material:material:1.0.0'

    implementation 'androidx.room:room-runtime:2.0.0'
    annotationProcessor 'androidx.room:room-compiler:2.0.0'

    androidTestImplementation 'androidx.room:room-testing:2.0.0'

    releaseImplementation 'com.crashlytics.sdk.android:crashlytics:2.9.6'
    releaseImplementation 'com.google.firebase:firebase-core:16.0.5'
    releaseImplementation 'com.google.firebase:firebase-perf:16.2.1'

    implementation 'org.apmem.tools:layouts:1.10@aar'

    implementation 'com.jakewharton.threetenabp:threetenabp:1.1.1'

    implementation 'com.jjoe64:graphview:4.2.2'

    pureImplementation 'ch.acra:acra-mail:5.2.1'
    pureImplementation 'ch.acra:acra-toast:5.2.1'
    pureImplementation 'ch.acra:acra-limiter:5.2.1'

    androidTestImplementation 'androidx.test.ext:junit:1.0.0'

    implementation 'com.github.PhilJay:MPAndroidChart:v3.0.3'
}

coveralls {
    sourceDirs += "${rootDir}/app/src/main/java/"
    //Unit tests
    jacocoReportPath "${buildDir}/reports/jacoco/debug/jacoco.xml"
    //Instrument tests
    //jacocoReportPath "${buildDir}/reports/coverage/debug/report.xml"
}

//ref: https://stackoverflow.com/a/49739351/3582371
if (getGradle().getStartParameter().getTaskNames().toString().toLowerCase().contains('release')) {
    apply plugin: 'com.google.gms.google-services'
}
